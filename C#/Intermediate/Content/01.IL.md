# 목차

1. [C#과 CL](#-C#과-CL)
2. [IL 프로그래밍](#-IL-프로그래밍)

<br>

# C#과 CL

C/C++의 문제점

C/C++로 작성된 프로그램 -> C/C++ 컴파일러 -> 실행 가능한 파일(Cpu가 이해하는 기계어로 구성)

- 특정 환경(Intel CPU, Window OS)에서만 동작한다

- 다른 환경에서 사용하려면 "다시 컴파일 하거나, 소스 코드 자체를 다시 작성" 해야한다.

C# 프로그램

C# 으로 작성된 프로그램 -> C# 컴파일러 -> 실행 가능한 파일(CPU와 OS에 독립적)

여기서 CPU와 OS에 독립적인 파일을 중간언어(IL : Intermediate Language)이라고 한다.

> JAVA = Byte Code | C# : IL

실행 가능한 파일은 독립적으로 실행되는것이 아닌 가상 머신을 통해 다양한 OS에서 실행될 수 있다.

.Net에서는 C++, VB등의 언어를 지원하기도 한다.

> 이때 사용되는 가상머신의 이름을 CLR(Common Language Runtime)이라고 한다.

# IL 프로그래밍

소스코드 작성과 빌드

- 확장자는 .il

- 빌드시 `ilasm ex01.il`

- .entrypoint는 꼭 하나만 있어야 한다.

- int 와 같은 별칭은 사용 불가능하며 int32와 같이 온전한 모양으로 작성해야 한다.

- 인자를 넣을때는 Stack Frame에 넣어서 인자를 전달한다.

  이때 코드는 `ldc.i4.1` 이며, `push`와 동일.

- nameSapce에서 Class 접근할 때는 .으로 접근

- Class 안의 정적 메서드 접근할때는 ::

  ```
  // C# : System.Console.WriteLine()
  call void [mscorlib]System.Console::WriteLine()

  // C# : System.Console.WriteLine("Hello, IL")
  ldstr "HELLO, IL"
  call void [mscorlib]System.Console::WriteLine(class System.String)
  ```

- 인자가 있는 함수를 받을 경우

  ```
  .assembly ex02 { }
  .assembly extern mscorlib {}

  // int x = 2;
  // int y = 20;
  // Console.WriteLine("{0}, {1}", x, y)

  .method static void foo() cil managed
  {
  .entrypoint

      .locals init(int32 x, int32 y)

      ldc.i4.2        // push 2
      stloc.0         // x = StackFram.top(2)
      ldc.i4.s 20     // push 20
      stloc.1         // y = StackFrame.top(20)

      ldstr "{0}, {1}"// string
      ldloc.0         // x
      ldloc.1         // y

      call void [mscorlib]System.Console::WriteLine(string, object, object)


      ret

  }
  ```

❗ 출력이 정상적으로 되지 않음.

- Boxing을 자동 지원하지 않기 떄문 (int x인데, object로 전달)

  즉 명시적으로 boxing해줘야함

  ```
  ldstr "{0}, {1}"// string
  ldloc.0         // x
  box int32       // boxing
  ldloc.1         // y
  box int32       // boxing
  ```

# IL과 객체 지향

IL또한 class와 접근 제한자를 지원한다.

```
.assembly ex02 { }
.assembly extern mscorlib {}


.class public Program
{
    .method public tatic void foo() cil managed
    {
        .entrypoint

        .locals init(int32 x, int32 y)

        ldstr "foo"
        call void [mscorlib]System.Console::WriteLine(string)

        ret
    }
}
```

C++과 기타 언어와 다르게 Main함수에서 시작되지 않는다. (아래는 Foo에서 시작)

```
.assembly ex02 { }
.assembly extern mscorlib {}


.class public Program
{
    .method public static void foo() cil managed
    {
        .entrypoint

        .locals init(int32 x, int32 y)

        ldstr "foo"
        call void [mscorlib]System.Console::WriteLine(string)

        ret
    }

    .method public static void Main() cil managed
    {
        .locals init(int32 x, int32 y)

        ldstr "Main"
        call void [mscorlib]System.Console::WriteLine(string)

        ret
    }
}
```

만약 지역변수의 멤버 함수를 호출하고 싶을 경우

```
.assembly ex02 { }
.assembly extern mscorlib {}


.class public Program
{
    //instance로 인스턴스임을 알린다.
    .method public instance void foo() cil managed
    {

        .locals init(int32 x, int32 y)

        ldstr "foo"
        call void [mscorlib]System.Console::WriteLine(string)

        ret
    }

    .method public specialname rtspecialname void .ctor() cil managed
    {
        //기반 클래스 생성자 호출
        ldarg.0
        call instance void [mscorlib]System.Object::.ctor()

        ret
    }

    .method public static void Main() cil managed
    {
        .entrypoint

        // 지역변수 선언
        // Program p
        .locals init( class Program V_0 )

        // 클래스를 위해 힙에 객체를 만들고 생성자를 만든다.
        newobj instance void Program::.ctor()

        // 객체의 주소를 Program V_0에 넣는다.
        // 객체의 주소는 newobj에서 stack의 top에 넣어진다.
        stloc.0

        // 현재 스택의 TOP 주소를 받아서 해당 인스턴스의 Foo를 호출함
        ldloc.0
        callvirt void Program::foo()


        ret
    }
}
```
